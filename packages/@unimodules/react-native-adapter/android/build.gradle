// apply from: project(":unimodules-core").file("../unimodules-core.gradle")
apply from: '../scripts/autolinking.gradle'

buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.1'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'org.unimodules'
version = '5.0.0'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

//Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

//Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

//Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: mavenLocal().url)
    }
  }
}

task generatePackageList {
  doLast {
    generateUnimodulesPackageList()
  }
}

preBuild.dependsOn generatePackageList

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 28)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 28)
    versionCode 13
    versionName "5.0.0"
  }
  lintOptions {
    abortOnError false
  }
  sourceSets {
    main {
      java {
         srcDirs += unimodules.getGeneratedSourceDir()
      }
    }
  }
}

dependencies {
  // Automatically add dependencies to all unimodules included by `unimodules.includeProjects` in `settings.gradle` file.
  unimodules.addDependencies(project)

  implementation 'com.facebook.react:react-native:+'
}
